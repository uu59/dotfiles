# vim: set ft=zsh:

_UPDATE_RUBY_VER=1

prompt_uu59_setup () {
  autoload -Uz add-zsh-hook
  autoload -Uz vcs_info
  zstyle ':vcs_info:*' max-exports 3
  zstyle ':vcs_info:*' enable git svn hg
  zstyle ':vcs_info:*' formats '%s:[%b]'
  # %m is expanded to empty string if hg-mg/stgit are unavailable
  zstyle ':vcs_info:*' actionformats '%s[%b]' '%m' '<⚑ %a>'
  zstyle ':vcs_info:(svn|bzr):*' branchformat '%b:r%r'

  PROMPT2="%B%{%F{082%}%__> %b"

  add-zsh-hook precmd _uu59_update_prompt
  add-zsh-hook preexec _uu59_detect_change_ruby
  add-zsh-hook preexec _uu59_cleanup_hooks
}

_uu59_cleanup_hooks () {
  if ! (prompt -c | grep -q -F -w uu59); then
    add-zsh-hook -d precmd _uu59_update_prompt
    add-zsh-hook -d preexec _uu59_detect_change_ruby
    add-zsh-hook -d preexec _uu59_cleanup_hooks
    return
  fi
}

_uu59_detect_change_ruby () {
  # rbenv version-name is slow (0.1 sec for me)
  # so don't call it if its unnecessary
  if (echo "rbenv rvm rm cd" | grep -q -w "${2%% *}") ;then
    # `rbenv version-name` returns incorrect version because it will be called before command completed
    _UPDATE_RUBY_VER=1
  fi
}

_uu59_update_prompt () {

  if [ $_UPDATE_RUBY_VER = "1" ];then
    _UPDATE_RUBY_VER=0
    PROMPT="%n%{%F{${MY_COLOR_PROMPT_HOST:-207}%}@%m%f:%~$(ruby_version)%(!.#.$) "
  fi

  LANG=C vcs_info
  if [ -z "$vcs_info_msg_0_" ];then
    RPROMPT=""
    return 0
  fi

  # 0: main
  # 1: misc
  # 2: rebase/merge
  local prompt="%{%F{${MY_COLOR_PROMPT_GIT_0:-190}%}$(git_clean_or_dirty)%f"
  prompt+="%F{${MY_COLOR_PROMPT_GIT_0:-190}%}$vcs_info_msg_0_%f"
  if [ -n "$vcs_info_msg_2_" ]; then
    prompt+=" %F{${MY_COLOR_PROMPT_GIT_2:-189}%}$vcs_info_msg_2_%f"
  fi
  RPROMPT=$prompt
}

# -- ruby_version() {{{
ruby_version() {
  local version=""
  if [ $+commands[rbenv] -ne 0 ];then
    version=$(rbenv version-name)
  elif [ $+commands[rvm-prompt] -ne 0 ]; then
    version=$(rvm-prompt)
  elif [ $+commands[ruby] -ne 0 ] ;then
    version=$(ruby -e 'print %Q!#{RUBY_VERSION}-p#{RUBY_PATCHLEVEL}!')
  fi

  if [ -n "$version" ];then
    echo " %{%F{248%}(ruby-$version)%f "
  fi
}
# }}}

# -- git_clean_or_dirty() {{{
git_clean_or_dirty() {
  # https://gist.github.com/sindresorhus/3898739
  # https://github.com/sindresorhus/pure/blob/master/prompt.zsh
  \git rev-parse --is-inside-work-tree &>/dev/null || return

  # following line can't detect untracked files (but fast on huge repository)
  # \git diff --quiet --ignore-submodules HEAD 2> /dev/null

  if [ -z "$(git status --porcelain -uall 2> /dev/null)" ];then
    echo ""
  else
    #all=$(echo $STATUS | wc -l )
    #tracked=$(git status --porcelain -uno | wc -l)
    #diff=$(expr $all - $tracked)
    #echo "%F{009%}✦ $tracked$(if [ $diff -gt 0 ]; then echo " (+$diff)";fi) %f"
    echo "%F{009%}✦ %f"
  fi
}
# }}}

prompt_uu59_setup "$@"
