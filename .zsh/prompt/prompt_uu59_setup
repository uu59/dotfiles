# vim: set ft=zsh:

prompt_uu59_setup () {
  autoload -Uz add-zsh-hook
  autoload -Uz vcs_info
  zstyle ':vcs_info:*' max-exports 3
  zstyle ':vcs_info:*' enable git svn hg
  zstyle ':vcs_info:*' formats '%s:[%b]'
  # %m is expanded to empty string if hg-mg/stgit are unavailable
  zstyle ':vcs_info:*' actionformats '%s[%b]' '%m' '<⚑ %a>'
  zstyle ':vcs_info:(svn|bzr):*' branchformat '%b:r%r'

  PROMPT2="%B%{%F{082%}%__> %b"

  add-zsh-hook precmd _uu59_update_prompt
  add-zsh-hook precmd _uu59_cleanup_hooks
  _uu59_update_prompt
}

_uu59_cleanup_hooks () {
  if [ "$prompt_theme" != "uu59" ];then
    add-zsh-hook -d precmd _uu59_update_prompt
    add-zsh-hook -d precmd _uu59_cleanup_hooks
  fi
}

_uu59_update_prompt () {
  # ruby version
  local ruby_path=$(readlink -e $(command -v ruby))
  local ruby_ver
  zstyle -s ':ruby:version' "$ruby_path" ruby_ver
  if [ -n "$ruby_path" ];then
    if [ -z "$ruby_ver" ];then
      ruby_ver="$(current_ruby_version)"
      zstyle ':ruby:version' "$ruby_path" "$ruby_ver"
    fi
  fi
  if [ -n "$ruby_ver" ];then
    ruby_ver=" %{%F{248%}(ruby-$ruby_ver)%f "
  fi

  PROMPT="%n%{%F{${MY_COLOR_PROMPT_HOST:-207}%}@%m%f:%~${ruby_ver}%(!.#.$) "

  LANG=C vcs_info
  if [ -z "$vcs_info_msg_0_" ];then
    RPROMPT=""
    return 0
  fi

  # 0: main
  # 1: misc
  # 2: rebase/merge
  local prompt="%{%F{${MY_COLOR_PROMPT_GIT_0:-190}%}$(git_clean_or_dirty)%f"
  prompt+="%F{${MY_COLOR_PROMPT_GIT_0:-190}%}$vcs_info_msg_0_%f"
  if [ -n "$vcs_info_msg_2_" ]; then
    prompt+=" %F{${MY_COLOR_PROMPT_GIT_2:-189}%}$vcs_info_msg_2_%f"
  fi
  RPROMPT=$prompt
}

current_ruby_version() {
  local version
  if [ $+commands[rbenv] -ne 0 ];then
    version=$(rbenv version-name)
  elif [ $+commands[rvm-prompt] -ne 0 ]; then
    version=$(rvm-prompt)
  elif [ $+commands[ruby] -ne 0 ] ;then
    version=$(ruby -e 'print %Q!#{RUBY_VERSION}-p#{RUBY_PATCHLEVEL}!')
  fi
  echo $version
}

# -- git_clean_or_dirty() {{{
git_clean_or_dirty() {
  # https://gist.github.com/sindresorhus/3898739
  # https://github.com/sindresorhus/pure/blob/master/prompt.zsh
  \git rev-parse --is-inside-work-tree &>/dev/null || return

  # following line can't detect untracked files (but fast on huge repository)
  # \git diff --quiet --ignore-submodules HEAD 2> /dev/null

  if [ -z "$(git status --porcelain -uall 2> /dev/null)" ];then
    echo ""
  else
    #all=$(echo $STATUS | wc -l )
    #tracked=$(git status --porcelain -uno | wc -l)
    #diff=$(expr $all - $tracked)
    #echo "%F{009%}✦ $tracked$(if [ $diff -gt 0 ]; then echo " (+$diff)";fi) %f"
    echo "%F{009%}✦ %f"
  fi
}
# }}}

prompt_uu59_setup "$@"
